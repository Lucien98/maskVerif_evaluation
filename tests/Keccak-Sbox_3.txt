proc NOT: (* !a *)
  inputs: a = a0 + a1 + a2 + a3
  outputs: d = d0 + d1 + d2 + d3;

  d0 := !a0;
  d1 := a1;
  d2 := a2;
  d3 := a3;

proc AND: (* a * b *)
  inputs: a = a0 + a1 + a2 + a3, b = b0 + b1 + b2 + b3
  outputs: c = c0 + c1 + c2 + c3
  randoms: r0, r1, r2, r3, r4, r5;

(* a0b0      (a0b1+r0) (a0b2+r1) (a0b3+r3)
   (a1b0+r0) a1b1      (a1b2+r2) (a1b3+r4)
   (a2b0+r1) (a2b1+r2) a2b2      (a2b3+r5)
   (a3b0+r3) (a3b1+r4) (a3b2+r5) a3b3 *)

  t1 = [a0*b1 + r0];
  t2 = [a0*b2 + r1];
  t3 = [a0*b3 + r3];
  c0 := a0*b0 + t1 + t2 + t3;

  t4 = [a1*b0 + r0];
  t5 = [a1*b2 + r2];
  t6 = [a1*b3 + r4];
  c1 := a1*b1 + t4 + t5 + t6;

  t7 = [a2*b0 + r1];
  t8 = [a2*b1 + r2];
  t9 = [a2*b3 + r5];
  c2 := a2*b2 + t7 + t8 + t9;

  t10 = [a3*b0 + r3];
  t11 = [a3*b1 + r4];
  t12 = [a3*b2 + r5];
  c3 := a3*b3 + t10 + t11 + t12;


proc Ksbox1: (* a + !b*c *)
  inputs: a = a0 + a1 + a2 + a3, b = b0 + b1 + b2 + b3, c = c0 + c1 + c2 + c3
  outputs: d = d0 + d1 + d2 + d3
  shares: nb= nb0 + nb1 + nb2 + nb3, nbc = nbc0 + nbc1 + nbc2 + nbc3;

  nb = NOT(b);
  nbc = AND(nb, c);
  d0 = [a0 + nbc0]; 
  d1 = [a1 + nbc1]; 
  d2 = [a2 + nbc2]; 
  d3 = [a3 + nbc3];
  
proc Ksbox: 
  inputs: a = a0 + a1 + a2 + a3, b = b0 + b1 + b2 + b3, c = c0 + c1 + c2 + c3, 
          d = d0 + d1 + d2 + d3, e = e0 + e1 + e2 + e3
  outputs: f = f0 + f1 + f2 + f3, g = g0 + g1 + g2 + g3, h = h0 + h1 + h2 + h3, 
           k = k0 + k1 + k2 + k3, l = l0 + l1 + l2 + l3;

  f = Ksbox1(a,b,c);
  g = Ksbox1(b,c,d);
  h = Ksbox1(c,d,e);
  k = Ksbox1(d,e,a);
  l = Ksbox1(e,a,b);
